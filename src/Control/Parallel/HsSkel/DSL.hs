{-# LANGUAGE GADTs #-}
{-# LANGUAGE Arrows #-}
{-# LANGUAGE MultiParamTypeClasses #-}
{-# LANGUAGE FlexibleInstances #-}
{-# LANGUAGE KindSignatures #-}
{-# LANGUAGE FunctionalDependencies #-}
{-# LANGUAGE TypeOperators #-}
{-# OPTIONS_HADDOCK show-extensions #-}

-- | This module defines the DSL to construct Streams and Skeletons.
module Control.Parallel.HsSkel.DSL (
    -- * Types
    DIM(..),
    Z,
    (:.),
    dimHead,
    dimTail,
    Future(),
    Skel(..),
    Stream(..),
    stDim,
    -- * Execution
    ExecutionContext(exec),
    -- * Skeleton Smart Constructors
    skSeq,
    SkParSupport(skPar),
    skSync,
    skMap,
    skRed,
    -- * Stream Smart Constructors
    StGenSupport(stGen),
    stMap,
    stChunk,
    stUnChunk,
    stStop,
    -- * Miscellaneous
    stFromList,
    skConst,
    skMapF,
    skPairF,
    skTraverseF,
    skDaC
) where

import Data.Traversable (Traversable)
import Control.Arrow (Arrow(arr, first, second, (***)), ArrowChoice(left, right, (+++)), ArrowApply(app), returnA)
import Control.Category (Category, id, (.))
import Control.DeepSeq (NFData)
import Prelude (Bool, Either, Int, Maybe(Just, Nothing), ($), Show, Read, Eq, Ord, (*))


{- ================================================================== -}
{- ============================= Types ============================== -}
{- ================================================================== -}


-- Indices y dimenciones, prestados de repa
data Z = Z
infixl 3 :.
data tail :. head
    = !tail :. !head
    deriving (Show, Read, Eq, Ord)

dimTail :: (DIM dim) => (dim :. Int) -> dim
dimTail (tail :. _) = tail

dimHead :: (DIM dim) => (dim :. Int) -> Int
dimHead (_ :. head) = head

class DIM dim where
    dimLinearSize :: dim -> Int

instance DIM Z where
    dimLinearSize _ = 1

instance DIM dim => DIM (dim :. Int) where
    dimLinearSize (tail :. head) = head * (dimLinearSize tail)


-- | This type represents a computation that is running in background.
-- Is not possible to handle futures directly and you need to use them inside the Skeletons DSL.
--newtype Future a = Future { mvar :: MVar a }
class Future (f :: * -> *)

-- | This data type represents the core of the Skeleton DSL.
-- 
-- A Skeleton is an abstraction over functions that allows to create common solutions to common problems. A type of @Skel i o@ should be readed as "an Skeleton that takes a value of type @i@ and generate an output of type @o@".
-- 
-- For every data type constructor that does not exists an arrow notation equivalence, exists a function with the same signature and name, but with the first letter in lowercase.
--
-- The data type constructors are exposed to other modules only to allow the creation of multiple execution schedulers, but the design of the DSL is focused to take advantage of the expression power of the arrow extension.
-- 
-- If you are not developing a new execution scheduler, then will be better to you use the smart constructors function and the arrow notation instead of using directly the data type constructors. For ensure doing that, add an import to 'Control.Parallel.HsSkel' instead of 'Control.Parallel.HsSkel.DSL'.
-- 
-- The constructors used in the arrow notation are:
-- 
-- * 'SkSeq_' is the same that 'arr' of 'Arrow'.
-- * 'SkComp' is the same that '.' of 'Category'
-- * 'SkPair' is the same that '***' of 'Arrow'
-- * 'SkChoice' is the same that '+++' of 'ArrowChoice' and is used in the if statement on the arrow notation.
-- * 'SkApply' is the same that 'app' of 'ArrowApply' and is used with the -<< operator in the arrow notation.
data Skel f i o where

    SkSeq    :: (NFData o, Future f) => (i -> o) -> Skel f i o
    SkSeq_   :: (Future f) => (i -> o) -> Skel f i o
    
    SkPar    :: (Future f) => Skel f i o -> Skel f i (f o)
    SkSync   :: (Future f) => Skel f (f i) i
    SkComp   :: (Future f) => Skel f x o -> Skel f i x -> Skel f i o
    SkPair   :: (Future f) => Skel f i1 o1 -> Skel f i2 o2 -> Skel f (i1, i2) (o1, o2)

    SkMap    :: (Traversable t, Future f) => Skel f i o -> Skel f (t i) (t o)
    SkChoice :: (Future f) => Skel f i o -> Skel f i' o' -> Skel f (Either i i') (Either o o')
    SkApply  :: (Future f) => Skel f (Skel f i o, i) o

    SkRed    :: (DIM dim, Future f) => Skel f (o, i) o -> Stream dim f i -> Skel f o o
    

-- | A Stream is like a pipeline that receives a flow of data and each single data pass across multiple parallel stages.
-- 
-- Every stage of the pipeline will be executed in parallel and depends of the execution scheduler implement some communication between the stages in order to avoid one stage produce data more quickly that the other can process that data. 
--
-- The first stage of Streams will be generated by the 'StGen' constructor. This constructor is the link between the user code and the Stream world.
--
-- A Stream cannot be infinite and is the programmer that must ensure the end of the Stream. An Stream can be finished by returning Nothing in the function of 'StGen' or by adding an 'StStop' stage that returns true. See the doc of 'stGen' and 'stStop' for more details.
--
-- The Stream and Skel world are connected by the 'skRed' smart constructor, that is like a fold over the stream applying an accumulator function and returning the result inside a Skeleton.
data Stream dim f d where
    StGen     :: (NFData o, Future f) => (i -> (Maybe (o, i))) -> i -> Stream Z f o
    StMap     :: (DIM dim, Future f) => dim -> Skel f i o -> Stream dim f i -> Stream dim f o
    StChunk   :: (DIM dim, Future f) => (dim :. Int) -> Int -> Stream dim f i -> Stream (dim:.Int) f i
    StUnChunk :: (DIM dim, Future f) => dim -> Stream (dim:.Int) f i -> Stream dim f i
    StStop    :: (DIM dim, Future f) => dim -> Skel f (c, i) c -> c -> Skel f c Bool -> Stream dim f i -> Stream dim f i

stDim :: Stream dim f d -> dim
stDim (StGen _ _) = Z
stDim (StMap dim _ _) = dim
stDim (StChunk dim _ _) = dim
stDim (StUnChunk dim _) = dim
stDim (StStop dim _ _ _ _) = dim


{- ================================================================== -}
{- ======================= Category and Arrow ======================= -}
{- ================================================================== -}

instance (Future f) => Category (Skel f) where
    id  = SkSeq_ id
    (.) = SkComp

instance (Future f) => Arrow (Skel f) where
    arr = SkSeq_
    first sk = sk `SkPair` id
    second sk = id `SkPair` sk
    (***) = SkPair
    --(&&&) sk1 sk2 = idem to default implementation.

instance (Future f) => ArrowChoice (Skel f) where
    left sk = SkChoice sk id
    right sk = SkChoice id sk
    (+++) = SkChoice
    --(|||) sk1 sk2 = idem to default implementation.

instance (Future f) => ArrowApply (Skel f) where
    app = SkApply


{- ================================================================== -}
{- ======================= Smart Constructors ======================= -}
{- ================================================================== -}

-- | Smart constructor for 'SkSeq'. This and 'arr' functions are the link between the user code and the Skeleton DSL.
-- 
-- This constructor creates an Skeleton that fully evaluates its output.
-- 
-- The main use of this constructor is for pass it to the 'skPar' and ensure that the code in the 'Future' fully evaluates the result.
skSeq :: (NFData o, Future f) => (i -> o) -- ^ A user defined function. 'NFData' is required for fully evaluates the output.
    -> Skel f i o
skSeq = SkSeq

-- | Smart constructor for 'SkSync'.
--
-- This constructor blocks until the input Future is completed and is the only way to get off a Future.
skSync :: (Future f) => Skel f (f i) i
skSync = SkSync

-- | Smart constructor for 'SkMap'.
-- 
-- This constructor is like 'Functor.fmap' but inside the Skeleton DSL. It takes a 'Traversable' structure and apply the Skeleton parameter to each element in the structure.
skMap :: (Traversable t, Future f) => Skel f i o -> Skel f (t i) (t o)
skMap = SkMap

-- | Smart constructor for 'SkRed'.
--
-- This constructor takes an accumulative Skeleton and a Stream and creates an Skeleton that takes an initial value and applies the accumulative Skeleton to each values of the Stream, returning the accumulated value.
--
-- Is like a fold for Stream but inside of the Skeleton DSL.
skRed :: (DIM dim, Future f) => Skel f (o, i) o -> Stream dim f i -> Skel f o o
skRed = SkRed

-- | Smart constructor for 'StMap'.
--
-- Takes a Skeleton and a Stream and returns a Stream with the same stages plus a new end stage that applies the Skeleton parameter to each value.
stMap :: (DIM dim, Future f) => Skel f i o -> Stream dim f i -> Stream dim f o
stMap sk st = StMap (stDim st) sk st

-- | Smart constructor for 'StChunk'.
--
-- Takes a size and a Stream a returns a Stream with the same stages plus a new end stage that collect @size@ values and put them in a vector.
-- Is useful if the work for processing each single value for the next stages is lower than the framework overhead.
stChunk :: (DIM dim, Future f) => Int -> Stream dim f i -> Stream (dim :. Int) f i
stChunk size st = StChunk ((stDim st) :. size) size st

-- | Smart constructor for 'StUnChunk'.
--
-- Allows to get of a previous 'stChuck' application by adding a new end stage that takes a 'Vector' of values and put each single value in the stream.
stUnChunk :: (DIM dim, Future f) => Stream (dim :. Int) f i -> Stream dim f i
stUnChunk st = StUnChunk (dimTail . stDim $ st) st

-- | Smart constructor for 'StStop'.
--
-- This constructor allow to stop the Stream outside the generator function used in the 'stGen'.
-- 
-- Is useful for problems in which put the logic of stop the stream in the generator should be too complex. For example, generate a Stream with the first 50 fibonnacci prime numbers. Using this constructor is possible to create an Stream that the generator gives fibonnacci numbers, the next stage verifies if the number is prime, and the next stop stage verifies if we have already 50 primes numbers.
stStop :: (DIM dim, Future f) => Skel f (c, i) c -> c -> Skel f c Bool -> Stream dim f i -> Stream dim f i
stStop acc z cond st = StStop (stDim st) acc z cond st

-- | This class allows to use the same function name for create an Skeleton from a function or another Skeleton.
class (Future f) => SkParSupport f a i o where
    -- | Smart constructor for 'SkPar'.
    -- 
    -- This constructor runs the computation parameter in background. This allows to get parallelism by running multiple computations in background.
    skPar :: a i o -- ^ A function (@i -> o@) or Skeleton (@Skel i o@)
	    -> Skel f i (f o)
    
instance (Future f) => SkParSupport f (Skel f) i o where
    skPar = SkPar

instance (NFData o, Future f) => SkParSupport f (->) i o where
    skPar = SkPar . SkSeq

-- | This class allows to use the same function name for create an Stream from a function that returns a Maybe directly or not.
class (Future f) => StGenSupport f fun i o where
    -- | Smart constructor for 'StGen'. This is the first stage for all Streams.
    --
    -- It takes an initial seed and a generator function that receives a seed and returns a pair with a value and another seed (see the instances). The stream is constructed by sequentially applying the generator function for the initial seed and the next generated seeds until the function returns 'Nothing'.
    stGen :: fun -> i -> Stream Z f o

instance (NFData o, Future f) => StGenSupport f (i -> (Maybe (o, i))) i o where
    stGen = StGen

instance (NFData o, Future f) => StGenSupport f (i -> (o, i)) i o where
    stGen f = StGen (Just . f)


{- ================================================================== -}
{- ======================= Execution Context ======================== -}
{- ================================================================== -}

class (Future f) => ExecutionContext ec m f | m -> ec, ec -> f where
    exec :: ec -> Skel f i o -> i -> m o


{- ================================================================== -}
{- ========================= Util Functions ========================= -}
{- ================================================================== -}

-- | Creates a Stream from a List. Requires 'NFData' of elements in order to fully evaluates the values.
stFromList :: (NFData a, Future f) => [a] -> Stream Z f a
stFromList l = StGen go l
    where
        go [] = Nothing
        go (x:xs) = Just (x, xs)


-- | Analogue to the 'Prelude.const' function but inside of Skeleton DSL. Returns a constant Skeleton that ignores the input and returns the parameter for all input.
skConst :: (Future f) => o -- ^ The constant value returned by the Skeleton.
    -> Skel f i o
skConst o = SkSeq_ (\_ -> o)

-- | This function allows to await for a future value and then execute another Skeleton, but without blocking the caller.
-- 
-- The block for the result and the apply of the other Skeleton will be done all in background.
skMapF :: (NFData o, Future f) => Skel f i o -> Skel f (f i) (f o)
skMapF sk = skPar $ sk . skSync

-- | This functions transform a pair of Future in a Future of pair.
skPairF :: (Future f) => Skel f (f o1, f o2) (f (o1, o2))
skPairF = skPar $ (***) skSync skSync

-- | This function allow transform a 'Traversable' structure of Future in a Future of the structure.
-- 
-- For example, using lists will it transforms a @[Future o]@ in a @Future [o]@.
skTraverseF :: (Traversable t, Future f) => Skel f (t (f o)) (f (t o))
skTraverseF = skPar $ skMap skSync

-- | A complex Skeleton composition example.
-- 
-- This is a Skeleton for the Divide & Conquer pattern. It takes an Skeleton that solves the base problem, a function that say if the input is the base case, and a split and merge functions and gives an Skeleton that applies the Divide & Conquer pattern.
skDaC :: (Traversable t, Future f) => 
    Skel f i o -- ^ Skeleton that resolves the base case.
    -> (i -> Bool) -- ^ A function that say if the input is the base case.
    -> (i -> t i) -- ^ A divide function strategy.
    -> (i -> t o -> o) -- ^ A merge function strategy.
    -> Skel f i o -- ^ A Skeleton that applies the D&C pattern.
skDaC skel isTrivial split combine = proc i -> do
    if (isTrivial i) 
        then
            skel -< i
        else do
            oSplit <- skMap skSync . skMap (skPar (skDaC skel isTrivial split combine)) -< split i
            returnA -< combine i oSplit

